# Imports
import sys
sys.path.append('../Vital.sandbox')
from utils import *


# Metadatas
name = "vital.sdk"
ARGUMENTS["target"] = f'template_{ARGUMENTS["build_type"].lower()}' 


# Environments
cwd = os.path.abspath(os.getcwd())
env = SConscript("../Vital.sandbox/SConstruct")
env = SConscript("Vendor/godot/SConstruct", {"env": env.Clone()})
if sys.platform.startswith("win"):
    env.Append(LINKFLAGS=['/NODEFAULTLIB:LIBCMTD'])
env.sources += (
    env.RGlob('Engine', '*.cpp') + 
    env.RGlob('Engine', '*.c') + 
    env.RGlob('Sandbox', '*.cpp') + 
    env.RGlob('Sandbox', '*.c')
)
env.Append(CPPPATH=[
    cwd,
    os.path.join(cwd, "../"),
    os.path.join(cwd, "Engine"),
    os.path.join(cwd, "Sandbox"),
    os.path.join(cwd, "Vendor")
])


# Builds
build_dir = ".build/{}".format(name)
env['SHLIBPREFIX'] = ''

VCPKG_ROOT = os.environ.get("VCPKG_ROOT")
if VCPKG_ROOT and sys.platform.startswith("linux"):
    VCPKG_LIB = os.path.join(VCPKG_ROOT, "installed", "x64-linux", "lib")
    env.Append(LIBS=[
        File(os.path.join(VCPKG_LIB, "librmlui.a")),
        File(os.path.join(VCPKG_LIB, "libfreetype.a")),
        File(os.path.join(VCPKG_LIB, "libpng.a")),
        File(os.path.join(VCPKG_LIB, "libz.a")),
        File(os.path.join(VCPKG_LIB, "libbz2.a")),
        File(os.path.join(VCPKG_LIB, "libbrotlidec.a")),
        File(os.path.join(VCPKG_LIB, "libbrotlicommon.a"))
    ])

build = env.SharedLibrary(
    ".bin/{}/{}".format(env['platform'], "{}{}{}".format(name, env["suffix"].replace(".dev", "").replace(".universal", ""), env.subst('$SHLIBSUFFIX'))),
    source = env.sources
)
Default(*[build, env.Install("{}/{}/".format(build_dir, env["platform"]), build)])