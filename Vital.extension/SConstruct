# Imports
import sys
sys.path.append("../Vital.sandbox")
from vital import *
from utils import *

os_info = Fetch_OS()

# Metadatas
name = "vital.sdk"
ARGUMENTS["target"] = f"template_{ARGUMENTS["build_type"].lower()}" 
ARGUMENTS["use_static_cpp"] = "no" 


# Environments
cwd = os.path.abspath(os.getcwd())
env = SConscript("../Vital.sandbox/SConstruct")
env = SConscript("Vendor/godot/SConstruct", {"env": env.Clone()})

if os_info["type"] == "Windows":
    env.Append(LINKFLAGS=["/NODEFAULTLIB:LIBCMTD", "/NODEFAULTLIB:MSVCRTD"])
else:
    env.Append(CXXFLAGS=["-fexceptions"])

env.sources += (
    env.RGlob("Engine", "*.cpp") + 
    env.RGlob("Engine", "*.c") + 
    env.RGlob("Sandbox", "*.cpp") + 
    env.RGlob("Sandbox", "*.c")
)
env.Append(CPPPATH=[
    cwd,
    os.path.join(cwd, "../"),
    os.path.join(cwd, "Engine"),
    os.path.join(cwd, "Sandbox"),
    os.path.join(cwd, "Vendor")
])


# Builds
build_dir = ".build/{}/{}".format(name, env["platform"])
bin_dir = ".bin/{}/{}".format(env["platform"], "{}{}{}".format(name, env["suffix"].replace(".dev", "").replace(".universal", ""), env.subst("$SHLIBSUFFIX")))

bin = env.SharedLibrary(bin_dir, source = env.sources)
build = env.Install(build_dir, bin)
env.Stage_VCPKG(build)


# Automated Discord SDK copy files
discord_sdk_bin = os.path.join(cwd, f"../Vital.sandbox/Vendor/discord-sdk/bin/{ARGUMENTS["build_type"].lower()}")
if os_info["type"] == "Windows":
    discord_lib = env.Install(build_dir, os.path.join(discord_sdk_bin, "discord_partner_sdk.dll"))
elif os_info["type"] == "Darwin":
    discord_lib = env.Install(build_dir, os.path.join(discord_sdk_bin, "libdiscord_partner_sdk.dylib"))
elif os_info["type"] == "Linux":
    discord_lib = env.Install(build_dir, os.path.join(discord_sdk_bin, "libdiscord_partner_sdk.so"))

Default([bin, build, discord_lib])