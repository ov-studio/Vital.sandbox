# Imports
from utils import *


# Metadatas
ARGUMENTS["platform_type"] = ARGUMENTS.get("platform_type", "Server")
ARGUMENTS["build_type"] = ARGUMENTS.get("build_type", "Debug")


# Environments
cwd = os.path.abspath(os.getcwd())
platform = sys.platform
env = Environment(tools=["default", "msvc"], PLATFORM="")
env.BuildConan(ARGUMENTS["build_type"])
env.MergeFlags(SConscript(".conan/SConscript_conandeps")["conandeps"])
env.sources = (
    env.RGlob("Type", "*.cpp") + 
    env.RGlob("System", "*.cpp") + 
    env.RGlob("Sandbox", "*.cpp") + 
    env.RGlob("Vendor", "*.cpp") + 
    env.RGlob("Vendor", "*.c") + 
    env.RGlob("Vendor", "*.cc")
)
env.Append(CPPPATH=[
    cwd,
    os.path.join(cwd, "Type"),
    os.path.join(cwd, "System"),
    os.path.join(cwd, "Sandbox"),
    os.path.join(cwd, "Vendor"),
    os.path.join(cwd, "Vendor/fmt"),
    os.path.join(cwd, "Vendor/msgpack"),
    os.path.join(cwd, "Vendor/rapidjson"),
    os.path.join(cwd, "Vendor/lua"),
    os.path.join(cwd, "Vendor/duktape")
])


# Flags
if ARGUMENTS["platform_type"] == "Client":
    env.Append(CPPFLAGS=["-DVital_SDK_Client"])


# Dependencies
env.Append(CPPFLAGS=["-DMSGPACK_NO_BOOST"])
VCPKG_TRIPLET = None
if platform.startswith("win"):
    VCPKG_TRIPLET = "x64-windows"
    env.Append(CPPFLAGS=["-DVital_SDK_WINDOWS", "/EHsc"])
    env.Append(LIBS=["ws2_32", "winmm", "wbemuuid"])
elif platform == "darwin":
    VCPKG_TRIPLET = "arm64-osx"
    env.Append(CPPFLAGS=["-DVital_SDK_MACOS"])
    env.Append(CPPDEFINES=["LUA_USE_MACOSX", "DUK_F_APPLE"])
elif platform.startswith("linux"):
    VCPKG_TRIPLET = "x64-linux"
    env.Append(CPPFLAGS=["-DVital_SDK_LINUX"])
    env.Append(CPPDEFINES=["LUA_USE_LINUX", "DUK_F_LINUX"])


VCPKG_ROOT = os.environ.get("VCPKG_ROOT")
if not VCPKG_ROOT:
    Exit("VCPKG_ROOT environment variable not set")

env.Append(CPPDEFINES=["VITAL_USE_VCPKG"])

env.Append(CPPPATH=[
    os.path.join(VCPKG_ROOT, "installed", VCPKG_TRIPLET, "include")
])

env.Append(LIBPATH=[
    os.path.join(VCPKG_ROOT, "installed", VCPKG_TRIPLET, "lib")
])

env.Append(LIBS=[
    "RmlCore",
    "RmlDebugger"
])
    
# Returns
Return("env")